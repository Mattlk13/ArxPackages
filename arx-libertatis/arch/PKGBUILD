# Maintainer: Daniel Scharrer <daniel@constexpr.org>

pkgname=arx-libertatis
pkgver=1.1
pkgrel=1
pkgdesc='Cross-platform port of Arx Fatalis, a first-person role-playing game (binaries only)'
url='http://arx-libertatis.org/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sdl' 'openal' 'freetype2' 'zlib' 'boost-libs' 'glew' 'libgl')
optdepends=('qt: enable built-in crash handler (recompile needed)'
            'gdb: generate detailed crash reports')
makedepends=('boost' 'cmake')
provides=('arx')
conflicts=('arx-git')
source=("https://github.com/downloads/arx/ArxLibertatis/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5719e1602051f6a6dc13ac0366ff03804f914ffbd855e7e06fdeb0d065fb6d32')
install='arx-libertatis.install'

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DUNITY_BUILD=ON
	
	# (Note: UNITY_BUILD speeds up the compilation and produces a more
	# optimized binary, but uses a lot of RAM - disable if low on memory.)
	
	# To get a debug build change -DCMAKE_BUILD_TYPE=Release to
	# -DCMAKE_BUILD_TYPE=Debug - this will run signifincantly slower but
	# enable more runtime checks and generate better crash reports.
	
	make -j`getconf _NPROCESSORS_ONLN`
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 README.md \
		"${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm644 CHANGELOG \
		"${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG"
	install -Dm644 AUTHORS \
		"${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
	install -Dm644 ARX_PUBLIC_LICENSE.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/ARX_PUBLIC_LICENSE.txt"
}
