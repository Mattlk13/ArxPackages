# Maintainer: Daniel Scharrer <daniel@constexpr.org>

pkgname=arx-libertatis-debug
realpkgname=arx-libertatis
pkgver=1.0_rc3
pkgrel=1
pkgdesc='Cross-platform port of Arx Fatalis (debug build)'
url='http://arx-libertatis.org/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sdl' 'devil' 'openal' 'freetype2' 'zlib' 'boost' \
         'glew' 'libgl' 'qt')
optdepends=('gdb: generate detailed crash reports')
makedepends=('cmake')
provides=('arx' 'arx-libertatis')
conflicts=('arx-git' 'arx-libertatis')
replaces=()
backup=()
options=(!strip)
source=(https://github.com/downloads/arx/ArxLibertatis/$realpkgname-$pkgver.tar.xz)
md5sums=(0ba6007cc04db78c5cf70996b31efafa)
sha1sums=(ac96b733e1f3d09a92db305466ff8e4ec5f58915)
sha256sums=(1810d3ad6ae8c5691ee9cd7c9d68c007569529cd50cbc2f75737e8c8b108e1b0)
install='arx-libertatis.install'

build() {
	cd "${srcdir}/${realpkgname}-${pkgver}"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DUNITY_BUILD=OFF -DCMAKE_BUILD_TYPE=Debug
	make -j`getconf _NPROCESSORS_ONLN`
}

package() {
	cd "${srcdir}/${realpkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 README.md \
		"$pkgdir/usr/share/doc/$realpkgname/README.md"
	install -Dm644 CHANGELOG \
		"$pkgdir/usr/share/doc/$realpkgname/CHANGELOG"
	install -Dm644 AUTHORS \
		"$pkgdir/usr/share/doc/$realpkgname/AUTHORS"
	license_file='ARX_PUBLIC_LICENSE.txt'
	install -Dm644 $license_file \
		"$pkgdir/usr/share/licenses/$realpkgname/$license_file"
}
