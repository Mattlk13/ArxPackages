# Maintainer: Daniel Scharrer <daniel@constexpr.org>

pkgname=innoextract
_installname=innoextract
pkgver=1.1
pkgrel=1
pkgdesc='A tool to extract installers created by Inno Setup'
url='http://innoextract.constexpr.org/'
arch=('i686' 'x86_64')
license=('ZLIB')
depends=('boost' 'xz')
optdepends=()
makedepends=('cmake')
conflicts=()
provides=()
options=()
source=("https://github.com/downloads/dscharrer/InnoExtract/${_installname}-${pkgver}.tar.gz")
sha256sums=('44d7bdf2e6366b25ecc96af3f775a998db7dcfba2445a825817358d9199b8dd2')

build() {
	cd "${srcdir}/${_installname}-${pkgver}"
	
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release
	
	make
}

package() {
	cd "${srcdir}/${_installname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 README.md \
		"${pkgdir}/usr/share/doc/${_installname}/README.md"
	install -Dm644 CHANGELOG \
		"${pkgdir}/usr/share/doc/${_installname}/CHANGELOG"
	install -Dm644 LICENSE \
		"${pkgdir}/usr/share/licenses/${_installname}/LICENSE"
}
