# Maintainer: Daniel Scharrer <daniel@constexpr.org>

pkgname=arx-libertatis
_installname=arx-libertatis
pkgver=1.0.2
pkgrel=1
pkgdesc='Cross-platform port of Arx Fatalis, a first-person role-playing game'
url='http://arx-libertatis.org/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sdl' 'devil' 'openal' 'freetype2' 'zlib' 'boost' 'glew' 'libgl')
optdepends=('qt: enable built-in crash handler (recompile needed)'
            'gdb: generate detailed crash reports')
makedepends=('cmake')
provides=('arx')
conflicts=('arx-git')
options=()
source=("https://github.com/downloads/arx/ArxLibertatis/${_installname}-${pkgver}.tar.xz")
sha256sums=('cd40510563f93149f237df55d373900f85eb05c9cc5731efe4d173f314d4fadd')
install='arx-libertatis.install'

build() {
	cd "${srcdir}/${_installname}-${pkgver}"
	
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DUNITY_BUILD=ON
	
	# (Note: UNITY_BUILD speeds up the compilation and produces a more
	# optimized binary, but uses a lot of RAM - disable if low on memory.)
	
	# To get a debug build change -DCMAKE_BUILD_TYPE=Release to
	# -DCMAKE_BUILD_TYPE=Debug - this will run signifincantly slower but
	# enable more runtime checks and generate better crash reports.
	
	make -j`getconf _NPROCESSORS_ONLN`
}

package() {
	cd "${srcdir}/${_installname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 README.md \
		"${pkgdir}/usr/share/doc/${_installname}/README.md"
	install -Dm644 CHANGELOG \
		"${pkgdir}/usr/share/doc/${_installname}/CHANGELOG"
	install -Dm644 AUTHORS \
		"${pkgdir}/usr/share/doc/${_installname}/AUTHORS"
	_license_file='ARX_PUBLIC_LICENSE.txt'
	install -Dm644 "${_license_file}" \
		"${pkgdir}/usr/share/licenses/${_installname}/${_license_file}"
}
